{
module NanoML.Compiler.Lexer
open Microsoft.FSharp.Text
open Microsoft.FSharp.Text.Lexing
open NanoML.Compiler.Parser


let lexeme = LexBuffer<_>.LexemeString

let inline nextline (lexbuf : LexBuffer<_>) =
    lexbuf.EndPos <- lexbuf.EndPos.NextLine
    lexbuf

let keywords =
    [ "if", IF
      "then", THEN
      "else", ELSE
      "fun", FUN
      "int", TINT
      "float", TFLOAT
      "bool", TBOOL
      "is", IS
      "let", LET
      "true", TRUE
      "false", FALSE ]

let kwdMap = keywords |> Map.ofList

let ident id =
    match Map.tryFind id kwdMap with
    | Some v -> v | _ -> VAR id

}

let ident = ['a' - 'z' 'A' - 'Z']+
let newline = '\n' | '\r' '\n'
let whitespace = [' ' '\t']
let digit = '\Nd'
let integer = digit+
let floatp = digit+ '.' digit*  
let floate = digit+ ('.' digit* )? ('e'| 'E') ['+' '-']? digit+
let float = floatp | floate 

rule token = parse
      whitespace { token lexbuf }
    | newline { nextline lexbuf; token lexbuf }
    | ident { ident (lexeme lexbuf) }
    | integer { INT(System.Int32.Parse(lexeme lexbuf)) }
    | float { FLOAT(System.Double.Parse(lexeme lexbuf)) }
    | "->" { TARROW }
    | ";;" { SEMICOLON2 }
    | '=' { EQUAL }
    | '<' { LESS }
    | ':' { COLON }
    | '(' { LPAREN }
    | ')' { RPAREN }
    | '+' { PLUS }
    | '-' { MINUS }
    | '/' { DIVIDE }
    | '*' { TIMES }
    | eof { EOF }
    | '\"' { STRING(string "" lexbuf.StartPos lexbuf) }
    | "(*" { comment lexbuf; token lexbuf }

and comment = parse
| "(*" { comment lexbuf; comment lexbuf }

| "*)" { () }

| '\n' { nextline lexbuf; comment lexbuf }

| _ { comment lexbuf }

and string s pos = parse
| "\\" ('n' | 'r' | 't' | '"') { let s' = s +
                                     match lexeme lexbuf with
                                     | "\\n" -> "\n" | "\\r" -> "\r"
                                     | "\\t" -> "\t" | "\\\"" -> "\""
                                     | _ -> failwith "not possible"
                                 string s' pos lexbuf }

| "\n" { nextline lexbuf; string (s + "\n") pos lexbuf }

| '\"' { s }

| eof { failwithf "unexpected end of string began at %A" pos }

| _ { string (s + lexeme lexbuf) pos lexbuf }